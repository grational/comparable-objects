/*
 * This build file was auto generated by 'gigawatt'
 * @author d7392
 * @date 21-03-2021 06.07
 */

// [plugins] this block must be the first
plugins {
	// to use the 'api' keyword in dependencies
	id 'java-library'
	// required by jitpack for libraries
	id 'maven-publish'
	// support for groovy
	id 'groovy'
	// autogenerate eclipse project files
	id 'eclipse'
	// create an html index linking every report generated
	id 'build-dashboard'
	// view of the project (jars used, navigable source tree)
	id 'project-report'
	// awesome git tag / gradle version integration
	id 'pl.allegro.tech.build.axion-release' version '1.18.18'
}

// [general] configuration
project.ext {
	groovyVersion = '4.0.26'
	spockVersion  = '2.3-groovy-4.0'
	slf4jVersion  = '2.0.17'
}

project.with {
	//project.name is set in the settings.gradle with rootProject.name
	group       = "it.grational.compare"
	version     = scmVersion.version
	description = "Classes used to ease objects comparisons"
}
// [crosscompile] source and target java version
java {
	sourceCompatibility = JavaVersion.toVersion('21')
	targetCompatibility = JavaVersion.toVersion('21')
}


repositories {
	mavenCentral()

	// leverage github as a maven repository
	maven { url = 'https://jitpack.io' }
}

dependencies {
	// Groovy language support
	api "org.apache.groovy:groovy:$groovyVersion"

	// Slf4j dependencies
	implementation "org.slf4j:slf4j-api:$slf4jVersion"


	// We use the awesome Spock testing and specification framework
	testImplementation "org.spockframework:spock-core:$spockVersion"
}

// [tasks] configuration //
jar {
	// execute check task when create the jar
	dependsOn 'check'
}

release {
	// test everything before a release
	dependsOn 'test'
}

// [tasks] definition
task listJars {
	description = 'print out the jars used by the project'
	doLast {
		configurations.compile.each { File file -> println file.name }
	}
}

tasks.withType(Test) {
	useJUnitPlatform()
	maxParallelForks = Runtime.runtime.availableProcessors() / 2
}
task uniTest(type: Test) {
	description = 'unit tests task'
	exclude '**/*ISpec*'
	exclude '**/*FSpec*'
}
task intTest(type: Test) {
	description = 'integration tests task'
	exclude '**/*USpec*'
	exclude '**/*FSpec*'
}
task funTest(type: Test) {
	description = 'functional tests task'
	exclude '**/*USpec*'
	exclude '**/*ISpec*'
}
gradle.startParameter.excludedTaskNames += "uniTest"
gradle.startParameter.excludedTaskNames += "intTest"
gradle.startParameter.excludedTaskNames += "funTest"

// required to work with jitpack.io
publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.github.grational'
			artifactId = 'comparable-objects'
			version = scmVersion.version // '0.5.0' (remove -SNAPSHOT for releases)
			from components.java
		}
	}
}
// vim: ft=groovy:fdm=indent
